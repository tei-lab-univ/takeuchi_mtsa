/*
This example is an interpretation of the MEDICAL(5) problem described for contingent planning 
in 'GPT: A Tool for Planning with Uncertainty and Partial Information'.
The problem involves a patient that can be healthy or may have N=1...5 diseases. There is a 
particular medication for each of the diseases, but if the patient receives the wrong one dies 
instead of being cured. 
In order to find which illness the patient has the are are two tests: stain and count cells. 
Stain has 3 possible results while count cells just high or normal. If the stain result is 1 or 2, 
we need to perform also count cells to desambiguate the results. When the result of the stain 3 the 
patient has the illness 5 despite the result of the count cells. 
Each of the illness is defined using def Illness(s,c), and the corresponding medication is medicate.[1..5].
*/
const N = 5
range Illness = 1..N 
range Medicine = 1..N
set High = {[1],[3],[5]}
set Normal = {[2],[4]}
set Stain1 = {[3],[4]}
set Stain2 = {[1],[2]}
set Stain3 = {[5]}
set Stain = {[1],[2],[3]}
const OK = 1
const H = 2
const NO_RES = 0
set Cells = {[OK],[H]}
set StainPlus = {Stain,[NO_RES]}
set CellsPlus = {Cells,[NO_RES]}


set Medicate = {medicate.[m:Medicine]}
set PersonResult = {cured, dead}
set Reset = {cured,dead}
set Controllable = {sStain, inspect, analyseBlood, sCountCells, Medicate}
set Uncontrollable = {eStain, resultS.Stain, bloodResult.Cells, eCountCells, PersonResult, leave, enter}
set Alphabet = {Controllable, Uncontrollable}

def Illness1(s,c) = (s==2 && c==H)
def Illness2(s,c) = (s==2 && c==OK)
def Illness3(s,c) = (s==1 && c==H)
def Illness4(s,c) = (s==1 && c==OK)
def Illness5(s,c) = (s==3 && c!=OK)
def Uncertainty(s,c) = !(Illness1(s,c) || Illness2(s,c) || Illness3(s,c) || Illness4(s,c) || Illness5(s,c))

MEDICATE = (enter -> RESULTS[NO_RES][NO_RES]),
RESULTS[s:StainPlus][c:CellsPlus] = (when Illness1(s,c) medicate.[m:Medicine] -> CERTAINTY[m][1] |
									 when Illness2(s,c) medicate.[m:Medicine] -> CERTAINTY[m][2] |
									 when Illness3(s,c) medicate.[m:Medicine] -> CERTAINTY[m][3] |
									 when Illness4(s,c) medicate.[m:Medicine] -> CERTAINTY[m][4] |
									 when Illness5(s,c) medicate.[m:Medicine] -> CERTAINTY[m][5] |
									 when Uncertainty(s,c) medicate.[m:Medicine] -> UNCERTAINTY  |
									 bloodResult.[rc:Cells] -> RESULTS[s][rc] |
									 resultS.[rs:Stain] -> RESULTS[rs][c]),
CERTAINTY[m:Medicine][i:Illness] =  (when (i!=m) dead -> DEATH |
									 when (i==m) cured -> leave-> EMPTY),
UNCERTAINTY = (cured -> leave -> EMPTY | dead -> DEATH), 
EMPTY = (cured -> EMPTY), 
DEATH = (dead -> DEATH).


PACIENT = (enter -> IN), 
IN = (cured -> HEALTHY | dead -> DEATH), 
HEALTHY = (leave -> EMPTY), 
EMPTY = (cured -> EMPTY),
DEATH = (dead -> DEATH).

TEST_STAIN = (sStain -> PROCESSING), 
PROCESSING = (eStain -> STOP).

TEST_CELLS = (sCountCells -> PROCESSING), 
PROCESSING = (eCountCells -> STOP).

DOCTOR_STAIN = (inspect -> INSPECTING),
INSPECTING = (resultS.[j:Stain] -> STOP).

DOCTOR_CELLS = (analyseBlood -> INSPECTING),
INSPECTING = (bloodResult.[j:Cells] -> STOP).


|| TESTS = (TEST_STAIN || TEST_CELLS).

|| DOCTOR = (DOCTOR_STAIN || DOCTOR_CELLS).

|| Game = (MEDICATE||TESTS||DOCTOR||PACIENT).

fluent Cured = <cured, Alphabet\{cured}>
fluent Dead = <dead, Alphabet\{dead}>

fluent FStain = <sStain, eStain>
fluent FCountCells = <sCountCells, eCountCells>
fluent FInspect = <inspect, resultS.Stain>
fluent FAnalyseBlood = <analyseBlood, bloodResult.Cells>
fluent FMedicates = <Medicate, cured>
fluent EndStain = <eStain, Reset>
fluent EndCountCells = <eCountCells, Reset>
fluent Pacient = <enter, Reset> 
fluent Work = <Controllable, Reset> 

ltl_property ANALYSE_AFTER_TEST = [](analyseBlood -> EndCountCells)
ltl_property INSPECT_AFTER_TEST = [](inspect -> EndStain)
ltl_property WORK_ONLY_IF_THERE_IS_A_PATIENT = [](Work -> Pacient)

assert Stain  = FStain
assert CountCells = FCountCells
assert Inspect  = FInspect
assert AnalyseBlood = FAnalyseBlood
assert Medicate = FMedicates

controllerSpec Goal = {
	safety = {ANALYSE_AFTER_TEST, INSPECT_AFTER_TEST, WORK_ONLY_IF_THERE_IS_A_PATIENT}
	liveness = {Cured}
  	controllable = {Controllable}
}

controller ||C = Game~{Goal}.

assert TESTGOAL = <> Cured

